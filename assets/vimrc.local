if exists("g:did_load_filetypes")
  filetype off
  filetype plugin indent off
endif
set rtp+=~/.vim/godoctor.vim
filetype plugin indent on
syntax on

" Pass selected text to Vipe. See:
"   https://vi.stackexchange.com/questions/11025/passing-visual-range-to-a-command-as-its-argument
function! VipeSelectedRange() range
  " Get the line and column of the visual selection marks
  let [lnum1, col1] = getpos("'<")[1:2]
  let [lnum2, col2] = getpos("'>")[1:2]

  " Get all the lines represented by this range
  let lines = getline(lnum1, lnum2)

  " The last line might need to be cut if the visual selection didn't end on the last column
  let lines[-1] = lines[-1][: col2 - (&selection == 'inclusive' ? 1 : 2)]
  " The first line might need to be trimmed if the visual selection didn't start on the first column
  let lines[0] = lines[0][col1 - 1:]

  " Get the desired text, and break multilines into multiple commands (yuck!)
  let selectedText = join(lines, " && ")

  " Do the call to Vipe
  :execute ":Vipe " . selectedText
endfunction

vnoremap ,x :call VipeSelectedRange()<CR>
